#
# qmake configuration for building with arm-linux-gnueabi-g++
#

MAKEFILE_GENERATOR      = UNIX
CONFIG                 += incremental
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

QT_CONFIG                += pocketbook

#Choose pocketbook QPA Plugin as default
QT_QPA_DEFAULT_PLATFORM = pocketbook


#QMAKE_CFLAGS_WARN_ON	= -Wall -W
#QMAKE_CFLAGS_WARN_OFF	= 
QMAKE_CFLAGS_RELEASE    = -O2 -fomit-frame-pointer -march=armv7-a -mtune=cortex-a8
QMAKE_CFLAGS_DEBUG      = -O0 -g -fno-omit-frame-pointer -march=armv7-a -mtune=cortex-a8
QMAKE_CFLAGS_SHLIB      = -fPIC
QMAKE_CFLAGS_YACC       = -Wno-unused -Wno-parentheses
QMAKE_CFLAGS_THREAD     = -D_REENTRANT
QMAKE_CFLAGS_HIDESYMS   = -fvisibility=hidden

QMAKE_CXXFLAGS          = $$QMAKE_CFLAGS -DQT_NO_QWS_TRANSFORMED -std=c++11
#QMAKE_CXXFLAGS_WARN_ON	= $$QMAKE_CFLAGS_WARN_ON
#QMAKE_CXXFLAGS_WARN_OFF	= $$QMAKE_CFLAGS_WARN_OFF
QMAKE_CXXFLAGS_RELEASE	+= $$QMAKE_CFLAGS_RELEASE
QMAKE_CXXFLAGS_DEBUG	+= $$QMAKE_CFLAGS_DEBUG
QMAKE_CXXFLAGS_SHLIB	= $$QMAKE_CFLAGS_SHLIB
QMAKE_CXXFLAGS_YACC     = $$QMAKE_CFLAGS_YACC
QMAKE_CXXFLAGS_THREAD	= $$QMAKE_CFLAGS_THREAD
QMAKE_CXXFLAGS_HIDESYMS = $$QMAKE_CFLAGS_HIDESYMS -fvisibility-inlines-hidden

DEFINES += QT_LINUXBASE  __arm__


FRSCSDK_DIR = $$(FRSCSDK_DIR)
isEmpty(FRSCSDK_DIR) {
    error("FRSCSDK_DIR environment variable not set")
}

QMAKE_INCDIR            = $${FRSCSDK_DIR}/arm-obreey-linux-gnueabi/sysroot/usr/local/include
QMAKE_INCDIR            += $${FRSCSDK_DIR}/arm-obreey-linux-gnueabi/sysroot/usr/include

QMAKE_INCDIR            += $${FRSCSDK_DIR}/arm-obreey-linux-gnueabi/sysroot/usr/include/freetype2

QMAKE_LIBDIR            = $${FRSCSDK_DIR}/arm-obreey-linux-gnueabi/sysroot/usr/local/lib
QMAKE_LIBDIR            += $${FRSCSDK_DIR}/arm-obreey-linux-gnueabi/sysroot/usr/lib


# modifications to g++.conf
QMAKE_CC                = arm-obreey-linux-gnueabi-gcc
QMAKE_CXX               = arm-obreey-linux-gnueabi-g++
QMAKE_LINK              = arm-obreey-linux-gnueabi-g++
QMAKE_LINK_SHLIB        = arm-obreey-linux-gnueabi-g++
QMAKE_LIBS		= -lrt

# modifications to linux.conf
QMAKE_AR                = arm-obreey-linux-gnueabi-ar cqs
QMAKE_OBJCOPY           = arm-obreey-linux-gnueabi-objcopy
QMAKE_STRIP             = arm-obreey-linux-gnueabi-strip
load(qt_config)
